Private Function list_combos(ByRef options_range As Range) As Variant()
Dim n, r, j
Dim options(), optioni
Dim combonames(), combo_n, indexes(), vals(), comboi
options = Application.Transpose(options_range)
n = options_range.Cells.Count
combo_n = 0
For r = 1 To n
  combo_n = combo_n + Application.Combin(n, r)
Next r
ReDim combonames(1 To combo_n)
comboi = 0
For r = 1 To n
  ReDim indexes(1 To r)
  ReDim vals(1 To r)
  '''first combo for r
  optioni = 0
  For j = 1 To r
    DoEvents
    optioni = optioni + 1
    indexes(j) = optioni
    vals(j) = options(optioni)
  Next j
  '''loop combos for r
  Do While optioni <= n
    DoEvents
    '''loop last j up to n
    j = r
    For optioni = optioni To n
      indexes(j) = optioni
      vals(j) = options(optioni)
      '''append combo
      comboi = comboi + 1
      combonames(comboi) = vals
    Next optioni
    '''backtrack j and setup next optioni
    Do While indexes(j) = n - (r - j)
      DoEvents
      j = j - 1
      If j = 0 Then Exit Do
    Loop
    If j = 0 Then Exit Do
    optioni = indexes(j)
    For j = j To r
      DoEvents
      optioni = optioni + 1
      indexes(j) = optioni
      vals(j) = options(optioni)
    Next j
  Loop
Next r
list_combos = combonames()
End Function
